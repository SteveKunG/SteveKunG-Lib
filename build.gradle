buildscript {
    repositories {
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

plugins {
    id "net.minecraftforge.gradle" version "5.1.+"
    id "io.github.CDAGaming.cursegradle" version "1.6.0"
}

apply plugin: "org.spongepowered.mixin"

version = project.version
group = "stevekung.mods.stevekunglib"
archivesBaseName = "SteveKunG's-Lib"
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: "stable", version: "${mcp_mapping}"
    accessTransformer = file("src/main/resources/META-INF/stevekunglib_at.cfg")

    def customJvms = [
            "-Dfml.coreMods.load=stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true",
            "-Dmixin.debug.export=true",
            "-Dmixin.debug.verbose=true"
    ]

    runs {
        client {
            workingDirectory project.file("run")

            if (project.hasProperty("username") && project.hasProperty("uuid")) {
                args "--username", "${username}", "--uuid", "${uuid}"
            }

            jvmArgs.addAll(customJvms)
            environment "MC_VERSION", project.mc_version

            mods {
                stevekungs_lib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            jvmArgs.addAll(customJvms)
            environment "MC_VERSION", project.mc_version

            mods {
                stevekungs_lib {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    maven {
        name "SpongePowered"
        url "https://repo.spongepowered.org/repository/maven-public/"
        content {
            includeGroup "org.spongepowered"
        }
    }
    maven {
        name "MixinBooter"
        url "https://maven.cleanroommc.com/"
        content {
            includeGroup "zone.rong"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    implementation "zone.rong:mixinbooter:7.0"
    embed "org.spongepowered:mixin:0.8.3"
    annotationProcessor "org.spongepowered:mixin:0.8.3:processor"
}

mixin {
    add sourceSets.main, "mixins.stevekung's_lib.refmap.json"
}

String fileName = "${archivesBaseName}-mc${mc_version}"

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "FMLCorePlugin": "stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
                   "ForceLoadAsMod": "true"
        )
    }
    archiveBaseName = fileName
    archiveVersion = "v" + project.version
}

tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allSource
    archiveBaseName = fileName
    archiveClassifier = "sources"
    archiveVersion = "v" + project.version
}

tasks.register("deobfJar", Jar) {
    from sourceSets.main.output
    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg",
                "FMLCorePluginContainsFMLMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "FMLCorePlugin": "stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
                "ForceLoadAsMod": "true"
        )
    }
    archiveBaseName = fileName
    archiveClassifier = "deobf"
    archiveVersion = "v" + project.version
}

artifacts {
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "296019"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion project.mc_version

            mainArtifact(jar) {
                displayName = fileName
                relations {
                    it.requiredDependency "mixin-booter"
                }
            }
            addArtifact sourcesJar
            addArtifact deobfJar
        }
    }
}

tasks.curseforge.dependsOn(build)
jar.finalizedBy("reobfJar")