buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.11"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.11"
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def keystore = new groovy.json.JsonSlurper().parseText(file("${keystore_path}").text)
def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle.forge"

version = project.version
group = "stevekung.mods.stevekunglib"
archivesBaseName = "SteveKunG's-Lib"
sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.12.2-14.23.4.2725"
    runDir = "run"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false

    replace "@FINGERPRINT@", keystore.sha1
    replaceIn "SteveKunGLib.java"
}

repositories {
    // Curse Gradle
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

jar {
    include "stevekung/mods/stevekunglib/**"
    include "META-INF/stevekunglib_at.cfg"
    include "license.txt"

    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg", "FMLCorePluginContainsFMLMod": "true")
    }
    baseName = "SteveKunG's-Lib-" + mcVersion
}

curseforge {
    apiKey = api.stevekung_lib
    project {
        id = "296019"
        changelog = "Change Log"
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion mcVersion

        mainArtifact(file("build/libs/SteveKunG's-Lib-" + mcVersion + "-" + version + ".jar")) {
            displayName = "SteveKunG's-Lib-" + mcVersion + "-" + version
        }
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = keystore.keystore_path
    alias = keystore.alias
    storePass = keystore.keystore_pass
    keyPass = keystore.keystore_pass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "deobf"
    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg", "FMLCorePluginContainsFMLMod": "true")
    }
}

build.dependsOn signJar