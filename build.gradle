buildscript {
    repositories {
        gradlePluginPortal()
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

def mcVersion = project.mc_version

version = project.version
group = "stevekung.mods.stevekunglib"
archivesBaseName = "SteveKunG's-Lib"
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: "snapshot", version: "${mcp_mapping}"
    accessTransformer = file("src/main/resources/META-INF/stevekunglib_at.cfg")

    def customJvms = [
            "-Dfml.coreMods.load=stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true",
            "-Dmixin.debug.export=true",
            "-Dmixin.debug.verbose=true"
    ]

    runs {
        client {
            workingDirectory project.file("run")
            args "--username", "${username}", "--uuid", "${uuid}"
            jvmArgs.addAll(customJvms)
            environment "MC_VERSION", mcVersion

            mods {
                stevekungs_lib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            jvmArgs.addAll(customJvms)
            environment "MC_VERSION", mcVersion

            mods {
                stevekungs_lib {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name "SpongePowered"
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    implementation "curse.maven:mixinBooter-419286:3321174"
    implementation "org.spongepowered:mixin:0.8.4"
    annotationProcessor "org.spongepowered:mixin:0.8.4:processor"
}

mixin {
    add sourceSets.main, "mixins.stevekung's_lib.refmap.json"
}

jar {
    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "FMLCorePlugin": "stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "SteveKunG's-Lib-" + mcVersion
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    baseName = "SteveKunG's-Lib-" + mcVersion
    classifier = "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    manifest {
        attributes("FMLAT": "stevekunglib_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "FMLCorePlugin": "stevekung.mods.stevekunglib.core.SteveKunGLibPlugin",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "SteveKunG's-Lib-" + mcVersion
    classifier = "deobf"
}

artifacts {
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "296019"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mcVersion

            mainArtifact(jar) {
                displayName = "SteveKunG's-Lib-" + mcVersion + "-" + version
                relations {
                    requiredLibrary "mixin-booter"
                }
            }
            addArtifact sourcesJar
            addArtifact deobfJar
        }
    }
}

tasks.curseforge.dependsOn(build)
jar.finalizedBy("reobfJar")